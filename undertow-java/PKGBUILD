maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
_groupId='io.undertow'
_postfix='.Final'
pkgver=1.4.23
pkgrel=1
pkgdesc='Undertow web server'
arch=('any')
url="http://undertow.io/"
license=('Apache')
makedepends=(
    'maven'
    'jdk8-openjdk'
)
depends=(
    'jboss-logging-java'
    'jboss-xnio-java'
    'java-environment'
    'slf4j-java'
    'wildfly-common-java'
)
_jar=(
    'undertow-core'
    'undertow-servlet'
    'undertow-websockets-jsr'
)

for _j in ${_jar[@]}; do
    source+=(
        "http://search.maven.org/remotecontent?filepath=${_groupId//.//}/${_j}/${pkgver}${_postfix}/${_j}-${pkgver}${_postfix}.jar"
        "http://search.maven.org/remotecontent?filepath=${_groupId//.//}/${_j}/${pkgver}${_postfix}/${_j}-${pkgver}${_postfix}.pom"
    )
    noextract+=(
        "${_j}-${pkgver}.jar"
        "${_j}-${pkgver}.pom"
    )
    releasepage+=("http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22${_groupId}%22%20AND%20a%3A%22${_j}%22")
done
sha512sums=('3c528c09a18f7192fbd3d779f15b9f0aaa2e0d337a3f57241477cfde6e98bca8234a73a4c75f6ed0bdab171d89ea1809e7f51c16695326996a079d9de9ec78c3'
            'b0bd9f57d72ec69ab560a311da72135128d8c92d39985ee345aa6746196e181972c0a2cce6ceeb5b71b40ee118da34957f8dedd9513f78683913ee4b6c875817'
            'b0f425463e33d01bb42441cde6791e01250002301bcdacb7861e8f301e1ca0a4a4a78dd6edebbff40f02e554814ea2afc0a3faf33e32fa4414bec41ae60b10de'
            'a3690c983f2bdc861a50cf1cbf34b698dfba2360638d7b61e367d2f5d12833c62589fbf8eb21cb56e519f3509a71eb5623c6e8bbd050fe82fca4384099b31690'
            '8b2ac5bb02cb4a8d65d85e3c10ab6f1225dc26bbc192ef363561bafa70fb6a09d95b86d6292b591371b1dbbc23f942294dce6e857bda6096dfcbbf853a631178'
            '868a34f8d52d1e6b08fa5515dd019df863dd84dcdc0baeced88aeab4959cb599170186417c269c2e78b45c0c277168edd411af7202dc307bbbcd4c11ee1d823c')

package() {
    for _j in ${_jar[@]}; do
        install -m644 -D ${srcdir}/${_j}-${pkgver}${_postfix}.{jar,pom} \
                      -t ${pkgdir}/usr/lib/java/${_groupId//.//}/${_j}/${pkgver}/
    done
}
