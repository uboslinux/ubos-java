maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
_name='postgresql'
_groupId='org.postgresql'
pkgver=42.2.15
pkgrel=2
pkgdesc='Postgresql JDBC driver'
arch=('any')
url="https://jdbc.postgresql.org/"
license=('GPL2')
depends=(
    'java-environment'
    'checkerframework-java'
)
source=("https://repo1.maven.org/maven2/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.pom"
        "https://repo1.maven.org/maven2/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.jar")

releasepage=('https://search.maven.org/artifact/org.postgresql/postgresql')
sha512sums=('5725ef81280093943a150b4f151d6719691562332b2dafb7a04e685d1438c4cb08d07ef6ab56d3254c3ed6561fb0807803c0fa8efd10b5d8c4572708fefd1835'
            'a853b5077f3ffe8527359ac137255a3382c2cd0cff2fe4b117285906f5ff6b390b8ea8698b572ac8671ded2a03022857902fa57a6ad4e893f7aca7684bc6a1f9')

prepare() {
    # This is a bit messy:
    # * The POM does not contain a groupId
    # * the META-INF directory in the jar contains a number of pom files and associated property files
    # * diet4j associates the wrong property file with the correct POM file
    #   ... and fills in the blank of the groupId with the wrong groupId from the wrong property file
    # We fix by adding in the groupId

    echo Patching
    perl -pi -e 's!(<artifactId>postgresql</artifactId>)!\1\n  <groupId>org.postgresql</groupId>!' ${srcdir}/${_name}-${pkgver}.pom
}

package() {
    install -m644 -D ${srcdir}/${_name}-${pkgver}.jar \
                     ${pkgdir}/usr/lib/java/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.jar
    install -m644 -D ${srcdir}/${_name}-${pkgver}.pom \
                     ${pkgdir}/usr/lib/java/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.pom
}
