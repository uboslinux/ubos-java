maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
_name='postgresql'
_groupId='org.postgresql'
pkgver=42.7.1
pkgrel=1
pkgdesc='Postgresql JDBC driver'
arch=('any')
url="https://jdbc.postgresql.org/"
license=('GPL2')
depends=(
    'java-environment'
    'checkerframework-java'
)
source=("https://repo1.maven.org/maven2/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.pom"
        "https://repo1.maven.org/maven2/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.jar")

releasepage=('https://search.maven.org/artifact/org.postgresql/postgresql')
sha512sums=('3987e5a36ac426046c253cefaa22fa4870ad0cf66591fd13f1c4a9c2068933a6b8ef2eb32fb0642b313287ed3e194c456a66e0e3866d5fefa808530cbf3d3750'
            '614942dc0a994a8913e4bc657de3e91e963a14849f4055470672e31a5bd3cefd01c4520dec2d1aa6702eb43c3f553cb6df77e3c36f95f001635e1e0ed0bc8822')

prepare() {
    # This is a bit messy:
    # * The POM does not contain a groupId
    # * the META-INF directory in the jar contains a number of pom files and associated property files
    # * diet4j associates the wrong property file with the correct POM file
    #   ... and fills in the blank of the groupId with the wrong groupId from the wrong property file
    # We fix by adding in the groupId

    echo Patching
    perl -pi -e 's!(<artifactId>postgresql</artifactId>)!\1\n  <groupId>org.postgresql</groupId>!' ${srcdir}/${_name}-${pkgver}.pom
}

package() {
    install -m644 -D ${srcdir}/${_name}-${pkgver}.jar \
                     ${pkgdir}/usr/lib/java/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.jar
    install -m644 -D ${srcdir}/${_name}-${pkgver}.pom \
                     ${pkgdir}/usr/lib/java/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.pom
}
