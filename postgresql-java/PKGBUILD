maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
_name='postgresql'
_groupId='org.postgresql'
pkgver=42.2.20
pkgrel=1
pkgdesc='Postgresql JDBC driver'
arch=('any')
url="https://jdbc.postgresql.org/"
license=('GPL2')
depends=(
    'java-environment'
    'checkerframework-java'
)
source=("https://repo1.maven.org/maven2/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.pom"
        "https://repo1.maven.org/maven2/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.jar")

releasepage=('https://search.maven.org/artifact/org.postgresql/postgresql')
sha512sums=('cb1cb0130ffb1dd524c7ddfc981c9defe0523915216d2dd67bd743315f238543f66ca2a3026543f40370e4049f7fe031e4980f384ed4dd5dcaa34e8a0f5f4a4d'
            'f7f48f6c69954fb1e7756cc1dbb984af17c00753741f83506673b5a9ad4e1d980f933a359529fef1ca63ca1f46f11a0bef9ef6c0d8b69d503a259dc31adfad91')

prepare() {
    # This is a bit messy:
    # * The POM does not contain a groupId
    # * the META-INF directory in the jar contains a number of pom files and associated property files
    # * diet4j associates the wrong property file with the correct POM file
    #   ... and fills in the blank of the groupId with the wrong groupId from the wrong property file
    # We fix by adding in the groupId

    echo Patching
    perl -pi -e 's!(<artifactId>postgresql</artifactId>)!\1\n  <groupId>org.postgresql</groupId>!' ${srcdir}/${_name}-${pkgver}.pom
}

package() {
    install -m644 -D ${srcdir}/${_name}-${pkgver}.jar \
                     ${pkgdir}/usr/lib/java/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.jar
    install -m644 -D ${srcdir}/${_name}-${pkgver}.pom \
                     ${pkgdir}/usr/lib/java/${_groupId//.//}/${_name}/${pkgver}/${_name}-${pkgver}.pom
}
