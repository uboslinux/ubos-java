# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# ALARM: Kevin Mihelich
# Brought up to date and simplified into one package by Indie Computing
pkgname=('java-jsvc')
pkgver=1.3.4
pkgrel=1
arch=('x86_64')
url='https://commons.apache.org/proper/commons-daemon/'
pkgdesc='Apache application for making Java applications run on UNIX more easily'
license=('APACHE')
_libname=commons-daemon
source=(https://archive.apache.org/dist/commons/daemon/source/${_libname}-${pkgver}-src.tar.gz{,.asc})
validpgpkeys=('A9C5DF4D22E99998D9875A5110C01C5A2F6059E7')
sha256sums=('df4849d05e5816610e67821883f4fc1e11724a0bb8b78b84b21edd5039ecebbe'
            'SKIP')
depends=('java-runtime' 'libcap')
makedepends=(
    'docbook2x'
    'docbook-utils'
    'docbook-xsl'
    'java-environment'
    'maven')

releasepage=('https://gitlab.archlinux.org/archlinux/packaging/packages/java-commons-daemon')
pkgverforked=('1.3.4-1')

build() {
  # commons-daemon.jar
  cd "${srcdir}/${_libname}-${pkgver}-src"
  mvn $MVN_OPTS clean package

  # jsvc
  cd "${srcdir}/${_libname}-${pkgver}-src/src/native/unix"
  ./configure --with-java=${JAVA_HOME:-/usr/lib/jvm/default}
  make
  # docbook2man man/jsvc.1.xml
  db2x_docbook2man man/jsvc.1.xml
}

package() {
  cd "${srcdir}/${_libname}-${pkgver}-src"

  install -Dm644 target/${_libname}-${pkgver}.jar -t ${pkgdir}/usr/share/java/
  ln -s ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}.jar

  cd "${srcdir}/${_libname}-${pkgver}-src/src/native/unix"

  install -Dm 755 jsvc -t ${pkgdir}/usr/bin/
  install -Dm 644 JSVC.1 -t ${pkgdir}/usr/share/man/man1/
}

