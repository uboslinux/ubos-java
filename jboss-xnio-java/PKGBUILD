maintainer="http://indiecomputing.com/"
pkgname=$(basename $(pwd))
_groupId='org.jboss.xnio'
_postfix='.Final'
pkgver=3.6.2
pkgrel=1
pkgdesc='JBoss xnio'
arch=('any')
url="http://jboss.org/"
license=('Apache')
depends=( 
    'java-environment'
    'jboss-threads-java'
    'wildfly-client-java'
) 
_rooturl="http://repo1.maven.org/maven2/${_groupId//.//}"

_modules=(
    xnio-api
    xnio-nio
)

source=()
for _m in "${_modules[@]}"; do
    source+=("${_rooturl}/${_m}/${pkgver}${_postfix}/${_m}-${pkgver}${_postfix}.jar");
done

releasepage=(
    'http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22org.jboss.xnio%22%20AND%20a%3A%22xnio-api%22'
    'http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22org.jboss.xnio%22%20AND%20a%3A%22xnio-nio%22')
sha512sums=('64b2e10e40c5d350f827e28f55c2667ecb96f217a8e54c5b7eb043f29fae868ceebeb487ccfb85a53c119b2450e61a034607be49db416b86b45f05ec934f243d'
            'b1719044893ddadf76d6b0e3346f979a3ddafdba5f640342a0275153293bb20f0e13aa4e811c595550b404d89f5509e3637a8fbdd9df1f7c29df5d7173a3a496')

noextract=("${source[@]##*/}")

package() {
    cd "${srcdir}"

    mkdir -p -m755 ${pkgdir}/usr/lib/java/${_groupId//.//}

    for _m in "${_modules[@]}"; do
        install -D -m644 "${_m}-${pkgver}${_postfix}.jar" "${pkgdir}/usr/lib/java/${_groupId//.//}/${_m}/${pkgver}/${_m}-${pkgver}${_postfix}.jar"
    done
}

